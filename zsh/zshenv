##
## ZSH environment setup (.zshenv)
##
zdotdir=$HOME

umask u=rwx,g=rx,o=rx
setopt noglobalrcs

# Set path to contain only unique elements, and then set a sensible default.
typeset -U path
path=( ~/.nodenv/bin(N)
       ~/.cargo/bin(N)
       ~/.local/bin(N)
       ~/bin(N)
       /opt/homebrew/{,s}bin(N)
       /usr/local/{,s}bin(N)
       /usr/{,s}bin(N)
       /{,s}bin(N)
     )

# Set cdpath
typeset -U cdpath
cdpath=( . ~/src ~/src/github.com/nickstenning )

# Profile
export EMAIL="Nick Stenning <nick@whiteink.com>"

# Editor
export EDITOR="vim"
export VISUAL="vim"
if which nvim >/dev/null 2>&1; then
  export EDITOR="nvim"
  export VISUAL="nvim"
fi
export TEXEDIT="${EDITOR} +%d %s"
export LESSEDIT="${EDITOR} ?lm+%lm. %f"
export PAGER="less"
export LESS="-R --ignore-case --long-prompt"

# Set UTF8 for terminals
unset LC_ALL
export LANG="en_US.UTF-8"
export LC_COLLATE="C"

# Even if you're on BSD, you'll want to leave the Linux version here for zsh
# colour completion.
export LS_COLORS="no=00:fi=00:di=33:ln=36:ex=00;31:pi=35;40:so=35;40:bd=35;40:cd=35;40"
export LSCOLORS="dxgxfafabxfafaxbxbadad"

# Virtualenv
export PIP_REQUIRE_VIRTUALENV=true
export VIRTUALENV_USE_DISTRIBUTE="yes"
export VIRTUAL_ENV_DISABLE_PROMPT=1 # we do our own prompt adjustment

# Go
export GOPATH="$HOME"

# Git
export GIT_DISCOVERY_ACROSS_FILESYSTEM=1

# Vagrant
export VAGRANT_DEFAULT_PROVIDER="vmware_fusion"

dot () {
  [ -f "$1" ] && . "$1"
}

# Homebrew
if which brew >/dev/null; then
  eval $(brew shellenv | grep -v '\bPATH=')
  export HOMEBREW_FORCE_BREWED_CURL=1

  # autoenv
  dot "$HOMEBREW_PREFIX/opt/autoenv/activate.sh"
  # chruby
  dot "$HOMEBREW_PREFIX/opt/chruby/share/chruby/chruby.sh"
  chruby ruby 2>/dev/null

  # pkg-config
  typeset -T PKG_CONFIG_PATH pkg_config_path
  typeset -U pkg_config_path
  pkg_config_path=( $HOMEBREW_PREFIX/{lib,share}/pkgconfig(/N) $pkg_config_path )
  export PKG_CONFIG_PATH
fi

# virtualenv
if which virtualenvwrapper_lazy.sh >/dev/null; then
  . "virtualenvwrapper_lazy.sh"
fi

# Google Cloud CLI
dot ~/src/google-cloud-sdk/path.zsh.inc
dot ~/src/google-cloud-sdk/completion.zsh.inc
export USE_GKE_GCLOUD_AUTH_PLUGIN=True

# Heroku
dot ~/Library/Caches/heroku/autocomplete/zsh_setup

# Debian
if [ "$(lsb_release -is 2>/dev/null)" = "Debian" ]; then
  dot /usr/share/virtualenvwrapper/virtualenvwrapper_lazy.sh
fi

if which nodenv >/dev/null; then
  eval "$(nodenv init -)"
fi

WHEEL_TOOL=$(which wheel)
if [ -x "$WHEEL_TOOL" ]; then
  export WHEEL_TOOL
fi
